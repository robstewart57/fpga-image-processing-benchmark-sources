/*
 * Copyright (c) 2014, Project: Rathlin, Heriot-Watt University, Edinburgh, UK
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the Heriot-Watt University nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
  
// Author: Kirsty Duncan

package Vivado;
import image.constants.constants.*;
 
 
actor createOutput1() uint(size=8) P, uint(size=8) R, uint(size=8) G, uint(size=8) B ==> uint(size=8) Rout, uint(size=8) Gout, uint(size=8) Bout :


	/* constants */
	uint(size=16) imageWidth = IM_WIDTH;
	uint(size=16) imageHeight = IM_HEIGHT;
	uint(size=8) fsSize = FS_SIZE;
	uint(size=8) FSdiv = 256/fsSize;
	/* how many row/column elements have been consumed */
	uint(size=16) xCount := 0;
	uint(size=16) yCount := 0;
	uint(size=16) zCount := 0;
	uint(size=16) aCount := 0;
	/* output */
	uint(size=8) imgR;
	uint(size=8) imgG;
	uint(size=8) imgB;

	int(size=16) peaks[fsSize][fsSize][fsSize][3];
	
	// store the input peaks in an array
	getPeaks: action P:[arrayVal] ==>
	do
		peaks[xCount][yCount][zCount][aCount] := arrayVal;
		aCount := aCount + 1;
		
		if (aCount = 3) then
			xCount := xCount + 1;
			aCount := 0;
		end
		if (xCount = fsSize) then
			yCount := yCount + 1;
			xCount := 0;
		end
		if (yCount = fsSize) then
			zCount := zCount + 1;
			yCount := 0;
		end
	end
	//stop when peaks are done
	donePeaks: action ==>
	guard zCount = fsSize
	do
		xCount := 0;
		yCount := 0;
		zCount := 0;
		aCount := 0;
	end
	
	// store the input image in an array and output the corrected image
	getRGB: action R:[arrayValR], G:[arrayValG], B:[arrayValB] ==> Rout:[imgR], Gout:[imgG], Bout:[imgB]
	do
		// output corrected RGB values
		imgR := peaks[arrayValR/FSdiv][arrayValG/FSdiv][arrayValB/FSdiv][0]*FSdiv;
		imgG := peaks[arrayValR/FSdiv][arrayValG/FSdiv][arrayValB/FSdiv][1]*FSdiv;
		imgB := peaks[arrayValR/FSdiv][arrayValG/FSdiv][arrayValB/FSdiv][2]*FSdiv;
		
		xCount := xCount + 1;
		
		if (xCount = imageWidth) then
			yCount := yCount + 1;
			xCount := 0;
		end
	end
	
	//when the full image has been output, stop
	doneRGB: action ==>
	guard yCount = imageHeight
	do
		xCount := 0;
		yCount := 0;
	end
	
	// schedule
	schedule fsm s0 :
		s0 (getPeaks) --> s0;
   		s0 (donePeaks) --> s1;   
   		s1 (getRGB) --> s1;
		s1 (doneRGB) --> s0;		 					
	end
	
	priority		
		donePeaks > getPeaks;
		doneRGB > getRGB;	
	end
	
end